set(QJS_SOURCES qjs.c "${CMAKE_CURRENT_SOURCE_DIR}/repl.c")
if(QJS_CONFIG_DEBUGGER)
  set_source_files_properties(${QJS_SOURCES} PROPERTIES LANGUAGE CXX)
  find_package(Boost REQUIRED)
endif()

add_executable(qjs ${QJS_SOURCES})
target_link_libraries(qjs quickjs)

if(QJS_CONFIG_DEBUGGER)
  set_property(TARGET qjs PROPERTY CXX_STANDARD 20)
endif()

execute_process(
  COMMAND cat "${PROJECT_SOURCE_DIR}/VERSION"
  OUTPUT_VARIABLE CONFIG_VERSION
  OUTPUT_STRIP_TRAILING_WHITESPACE)

target_compile_definitions(qjs PRIVATE CONFIG_VERSION="${CONFIG_VERSION}")

# make qjsc builds before qjs since qjs requires a file `repl.c` which is
# generated by the qjsc
add_dependencies(qjs qjsc)

set(QJSC "$<TARGET_FILE:qjsc>")

add_custom_command(
  OUTPUT "${CMAKE_CURRENT_SOURCE_DIR}/repl.c"
  COMMAND ${QJSC} ARGS -c -o "${CMAKE_CURRENT_SOURCE_DIR}/repl.c" -m
          "${CMAKE_CURRENT_SOURCE_DIR}/repl.js"
  DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/repl.js")

if(QJS_CONFIG_BIGNUM)
  if(QJS_CONFIG_DEBUGGER)
    set_source_files_properties("${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.c"
                                PROPERTIES LANGUAGE CXX)
  endif()
  target_sources(qjs PRIVATE "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.c")

  add_custom_command(
    OUTPUT "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.c"
    COMMAND ${QJSC} ARGS -fbignum -c -o "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.c"
            "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.js"
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.js")
endif()
