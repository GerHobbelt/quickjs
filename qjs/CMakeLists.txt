add_executable(qjs "qjs.c" "${CMAKE_CURRENT_SOURCE_DIR}/repl.c")
target_link_libraries(qjs quickjs)

execute_process(
  COMMAND cat "${PROJECT_SOURCE_DIR}/VERSION"
  OUTPUT_VARIABLE CONFIG_VERSION
  OUTPUT_STRIP_TRAILING_WHITESPACE)

target_compile_definitions(qjs PRIVATE CONFIG_VERSION="${CONFIG_VERSION}")

# make qjsc builds before qjs since qjs requires a file `repl.c` which is
# generated by the qjsc
add_dependencies(qjs qjsc)

set(QJSC "${PROJECT_BINARY_DIR}/qjsc/qjsc")

add_custom_command(
  OUTPUT "${CMAKE_CURRENT_SOURCE_DIR}/repl.c"
  COMMAND ${QJSC} ARGS -c -o "${CMAKE_CURRENT_SOURCE_DIR}/repl.c" -m
          "${CMAKE_CURRENT_SOURCE_DIR}/repl.js"
  DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/repl.js")

if(CONFIG_BIGNUM)
  target_sources(qjs PRIVATE "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.c")

  add_custom_command(
    OUTPUT "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.c"
    COMMAND ${QJSC} ARGS -fbignum -c -o "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.c"
            "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.js"
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/qjscalc.js")
endif()
